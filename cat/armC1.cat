ARM

include "cos.cat"
include "fences.cat"

(* Uniproc *)
let com = rf | fr | co
acyclic (po-loc | com) as uniproc

(*******)
(* ppo *)
(*******)
let dp = addr | data
let rdw = po-loc & (fre;rfe)
let detour = po-loc & (coe;rfe)

(* Initial value *)
let ii0 = dp | rfi | rdw
let ic0 = 0
let ci0 = ctrlisb | detour
let cc0 = dp | ctrl | (addr;po)

(* Fixpoint from i -> c in instructions and transitivity *)
let rec ii = ii0 | ci | (ic;ci) | (ii;ii)
and ic = ic0 | ii | cc | (ic;cc) | (ii;ic)
and ci = ci0 | (ci;ii) | (cc;ci)
and cc = cc0 | ci | (ci;ic) | (cc;cc)

let ppo = (R*W & ic) | (R*R & ii)

(**********)
(* fences *)
(**********)

let fence = ish

(* happens before *)
let hb = ppo | fence | rfe

(* prop *)
let propbase = (fence | (rfe;fence));hb*
let prop = (W*W & propbase)| (com*;propbase*;ish;hb*)
